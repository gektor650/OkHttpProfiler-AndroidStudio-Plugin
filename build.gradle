plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.3'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group 'com.itkacher.okhttpprofiler'
version '1.0.15'

repositories {
    mavenCentral()
    jcenter()
}

if (!hasProperty('StudioCompilePath')) {
    throw new GradleException("No StudioCompilePath value was set, please create gradle.properties file")
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.7'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    compileOnly fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])
}

intellij {
    pluginName 'OkHttpProfiler'
    updateSinceUntilBuild false

    plugins 'android'

    intellij.localPath = project.hasProperty("StudioRunPath") ? StudioRunPath : StudioCompilePath
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
patchPluginXml {
    changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}

task(verifySetup) {
    doLast {
        def ideaJar = "$StudioCompilePath/lib/idea.jar"
        if (!file(ideaJar).exists()) {
            throw new GradleException("$ideaJar not found, set StudioCompilePath in gradle.properties")
        }
    }
}

compileJava.dependsOn verifySetup
